plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.3.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'io.github.bytebeats'
version '0.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testImplementation fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    pluginName = 'OkHttpAnalyzer'
    updateSinceUntilBuild = false
    version = '211.7628.21'
    plugins = ['android']
    localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
    instrumentCode {
        compilerVersion = "211.7628.21"
    }
//
    instrumentTestCode {
        compilerVersion = "211.7628.21"
    }
}

runIde {
    // Absolute path to installed target 3.5 Android Studio to use as IDE Development Instance
    // The "Contents" directory is macOS specific.
    def dir = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
    ideDir = file(dir)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    changeNotes = """
                v0.1.0 the 1st release.<br>
                """
}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, please set StudioCompilePath in gradle.properties")
        }
    }
}

compileJava.dependsOn verifySetup
buildSearchableOptions.enabled = false